apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion androidCompileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId androidApplicationId
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode androidVersionCode
        versionName androidVersionName
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true
    }

    flavorDimensions "environment"

    productFlavors {
        development {
            dimension "environment"
            buildConfigField "String", "BASE_URL", "\"$devBaseUrl\""
        }
        production {
            dimension "environment"
            buildConfigField "String", "BASE_URL", "\"$prodBaseUrl\""
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':data')
    implementation project(':domain')

    implementation appDependencies.kotlinStd
    implementation appDependencies.appCompat
    implementation appDependencies.coreKtx
    implementation appDependencies.multidex
    implementation appDependencies.koinCore
    implementation appDependencies.koinScope
    implementation appDependencies.koinViewmodel
    implementation appDependencies.anko
    implementation appDependencies.material
    implementation appDependencies.lifecycleExtentions
    implementation appDependencies.lifecycleCommon
    kapt appDependencies.lifecycleCompiler
    implementation appDependencies.navigationUi
    implementation appDependencies.navigationFragment
    implementation appDependencies.navigationFeature
    implementation appDependencies.retrofit
    implementation appDependencies.retrofitConverterGson
    implementation appDependencies.retrofitCoroutinesAdapter
    implementation appDependencies.coroutinesCore
    implementation appDependencies.coroutinesAndroid
    implementation appDependencies.okHttp
    implementation appDependencies.glide
    kapt appDependencies.glideCompiler
    implementation appDependencies.roundedImages
    implementation appDependencies.constraint
    implementation appDependencies.timber
}
